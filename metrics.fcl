FUNCTION_BLOCK metrics

VAR_INPUT
	cpu : REAL;
	ram : REAL;
	disk : REAL;
	networkLatency : REAL;
	responseTime : REAL;
END_VAR

VAR_OUTPUT
	serverLoad : REAL;
	networkLoad : REAL;
END_VAR

//Fuzzify input variable cpu
FUZZIFY cpu
	TERM low := (0,1) (25,0);
	TERM medium := (25,0) (40,1) (60,1) (75,0);
	TERM high := (75,0) (100,1);
END_FUZZIFY

//Fuzzify input variable ram
FUZZIFY ram
	TERM low := (0,1) (25,0);
	TERM medium := (25,0) (40,1) (60,1) (75,0);
	TERM high := (75,0) (100,1);
END_FUZZIFY

//Fuzzify input variable disk
FUZZIFY disk
	TERM low := (0,1) (40,0);
	TERM medium := (35,0) (50,1) (75,1) (90,0);
	TERM high := (90,0) (100,1);
END_FUZZIFY

//Fuzzify input variable networkLatency
FUZZIFY networkLatency
	TERM good := sigm -7 1;
	TERM bad :=  sigm  7 1;
END_FUZZIFY

//Fuzzify input variable responseTime
FUZZIFY responseTime
	TERM low := (0,1) (1,0);
	TERM medium := (1,0) (2,1) (3,1) (4,0);
	TERM high := (4,0) (10,1);
END_FUZZIFY

//Defuzzify output variable decision
DEFUZZIFY serverLoad
	TERM low := (0,0) (12.5,1) (25,0);
	TERM medium := (25,0) (50,1) (75,0);
	TERM high := (75,0) (87.5,1) (100,0);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

//Defuzzify output variable decision
DEFUZZIFY networkLoad
	TERM low := (0,0) (12.5,1) (25,0);
	TERM medium := (25,0) (50,1) (75,0);
	TERM high := (75,0) (87.5,1) (100,0);
	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK rules
	AND : MIN;
	ACT : MIN;
	ACCU : MAX;
	
	RULE 1 : IF cpu IS low AND ram IS low AND responseTime IS low
			 THEN serverLoad IS low;
	RULE 2 : IF cpu IS medium OR ram IS medium
			 THEN serverLoad IS medium;
	RULE 3 : IF responseTime IS high AND (cpu IS high OR ram IS high)
			 THEN serverLoad IS high;
	RULE 4 : IF cpu IS high OR ram IS high
			 THEN serverLoad IS high;
	RULE 5 : IF networkLatency IS bad AND responseTime IS high
			 THEN networkLoad IS high;		 
	RULE 6 : IF networkLatency IS good AND responseTime IS low
			 THEN networkLoad IS low;
	RULE 7 : IF disk IS high AND cpu IS high AND ram IS high
			 THEN serverLoad IS high;
	RULE 8 : IF networkLatency IS good AND responseTime is medium
			 THEN networkLoad IS medium;
END_RULEBLOCK

END_FUNCTION_BLOCK
